---
page_title: "awscc_ec2_vpc_endpoint Resource - terraform-provider-awscc"
subcategory: ""
description: |-
  Resource Type definition for AWS::EC2::VPCEndpoint
---

# awscc_ec2_vpc_endpoint (Resource)

Resource Type definition for AWS::EC2::VPCEndpoint

## Example Usage

To create a VPC endpoint for S3
```terraform
#Basic

resource "awscc_ec2_vpc" "main" {
  cidr_block = "10.0.0.0/16"
}

resource "awscc_ec2_vpc_endpoint" "s3" {
  vpc_id       = awscc_ec2_vpc.main.id
  service_name = "com.amazonaws.us-west-2.s3"
}
```

### Interface Endpoint Type
To create a VPC Endpoint with Interface type
```terraform
#Interface Endpoint Type

resource "awscc_ec2_vpc" "main" {
  cidr_block           = "10.0.0.0/16"
  enable_dns_support   = true
  enable_dns_hostnames = true
}

resource "aws_security_group" "sg1" {
  name        = "allow_tls"
  description = "Allow TLS inbound traffic"
  vpc_id      = awscc_ec2_vpc.main.id

  ingress {
    description = "TLS from VPC"
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = [awscc_ec2_vpc.main.cidr_block]
  }

  egress {
    from_port        = 0
    to_port          = 0
    protocol         = "-1"
    cidr_blocks      = ["0.0.0.0/0"]
    ipv6_cidr_blocks = ["::/0"]
  }

  tags = {
    Name = "allow_tls"
  }
}

resource "awscc_ec2_vpc_endpoint" "ec2" {
  vpc_id            = awscc_ec2_vpc.main.id
  service_name      = "com.amazonaws.us-west-2.ec2"
  vpc_endpoint_type = "Interface"

  security_group_ids = [
    aws_security_group.sg1.id,
  ]

  private_dns_enabled = true
}
```

### Gateway Load Balancer Endpoint Type
To create a VPC Endpoint with Gateway LB
```terraform
data "aws_caller_identity" "current" {}

resource "awscc_ec2_vpc" "main" {
  cidr_block = "10.0.0.0/16"
}

resource "awscc_ec2_subnet" "main" {
  vpc_id     = awscc_ec2_vpc.main.id
  cidr_block = "10.0.1.0/24"
  availability_zone = "us-west-1c"
}

resource "aws_internet_gateway" "ig" {
  vpc_id     = awscc_ec2_vpc.main.id
}

resource "aws_lb" "test" {
  name               = "test-lb-tf"
  load_balancer_type = "gateway"
  subnets            = [awscc_ec2_subnet.main.id]
}

resource "aws_vpc_endpoint_service" "example" {
  acceptance_required        = false
  gateway_load_balancer_arns = [aws_lb.test.arn]
}

resource "awscc_ec2_vpc_endpoint" "example" {
  service_name      = aws_vpc_endpoint_service.example.service_name
  vpc_endpoint_type = aws_vpc_endpoint_service.example.service_type
  vpc_id            = awscc_ec2_vpc.main.id
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `service_name` (String) The service name.
- `vpc_id` (String) The ID of the VPC in which the endpoint will be used.

### Optional

- `policy_document` (String) A policy to attach to the endpoint that controls access to the service.
- `private_dns_enabled` (Boolean) Indicate whether to associate a private hosted zone with the specified VPC.
- `route_table_ids` (Set of String) One or more route table IDs.
- `security_group_ids` (Set of String) The ID of one or more security groups to associate with the endpoint network interface.
- `subnet_ids` (Set of String) The ID of one or more subnets in which to create an endpoint network interface.
- `vpc_endpoint_type` (String)

### Read-Only

- `creation_timestamp` (String)
- `dns_entries` (List of Dynamic)
- `id` (String) The ID of this resource.
- `network_interface_ids` (List of Dynamic)

## Import

Import is supported using the following syntax:

```shell
$ terraform import awscc_ec2_vpc_endpoint.example <resource ID>
```